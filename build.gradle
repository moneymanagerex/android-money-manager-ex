/*
 * Copyright (C) 2012-2018 The Android Money Manager Ex Project Team
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 3
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        google()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // robolectric
        maven { url "https://clojars.org/repo/" }               // icepick
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.0'
    }
}

ext {
    compileSdkVersion = 30
    buildToolsVersion = '30.0.2'
    supportLibrary = '1.0.0'

    // https://developer.android.com/topic/libraries/support-library/revisions.html
    supportDependencies = [
            core             : "androidx.legacy:legacy-support-core-utils:${supportLibrary}",
            support          : "androidx.legacy:legacy-support-v4:${supportLibrary}",
            support_compat   : "com.android.support:support-compat:${supportLibrary}",
            appCompat        : "androidx.appcompat:appcompat:${supportLibrary}",
            cardView         : "androidx.cardview:cardview:${supportLibrary}",
            design           : "com.google.android.material:material:${supportLibrary}",
            preference       : "androidx.legacy:legacy-preference-v14:${supportLibrary}",
            recyclerView     : "androidx.recyclerview:recyclerview:${supportLibrary}",
            supportAnnotation: "androidx.annotation:annotation:${supportLibrary}",
            gridLayout       : "androidx.gridlayout:gridlayout:${supportLibrary}"
    ]
}

// This applies to the projects underneath.
allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://clojars.org/repo/" }               // icepick
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // butterknife
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    /**
     * Force all dependent libs to use the same version of the libraries.
     * This is used in testing libraries (Espresso).
     * https://code.google.com/p/android-test-kit/issues/detail?id=136
     */
    configurations.all {
        resolutionStrategy {
            force 'org.hamcrest:hamcrest-core:1.3'
            force 'junit:junit:4.12'
            force supportDependencies.supportAnnotation
            force supportDependencies.appCompat
            force supportDependencies.design
            force 'com.google.code.gson:gson:2.8.2'
            force 'com.google.guava:guava:27.0.1-android'
            force 'com.google.code.findbugs:jsr305:3.0.1'
            force 'com.android.support:multidex:2.0.0'
        }
        // removes warnings about commons-logging on build.
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    //
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }
}
