/*
 * Copyright (C) 2012-2016 The Android Money Manager Ex Project Team
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 3
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }  // Fabric
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'io.fabric.tools:gradle:1.+'                  // Fabric
    }
}

plugins {
    // dependencyUpdates
    id 'com.github.ben-manes.versions' version '0.13.0'
}

ext {
    compileSdkVersion = 24
    buildToolsVersion = '25.0.2'

    // https://developer.android.com/topic/libraries/support-library/revisions.html
    supportLibrary = '25.1.0'
    supportDependencies = [
            support          :       "com.android.support:support-v4:${supportLibrary}",
            appCompat        :       "com.android.support:appcompat-v7:${supportLibrary}",
            cardView         :       "com.android.support:cardview-v7:${supportLibrary}",
            design           :       "com.android.support:design:${supportLibrary}",
            preference       :       "com.android.support:preference-v14:${supportLibrary}",
            recyclerView     :       "com.android.support:recyclerview-v7:${supportLibrary}",
            supportAnnotation:       "com.android.support:support-annotations:${supportLibrary}",
            gridLayout       :       "com.android.support:gridlayout-v7:${supportLibrary}"
    ]
}

// This applies to the projects underneath.
allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://maven.fabric.io/public' }          // fabric
        maven { url 'http://maven.cloudrail.com' }              // cloudrail
        maven {url "https://clojars.org/repo/"}                 // icepick
//        flatDir {
//            // use libs directory as a Maven repository.
//            dirs 'libs'
//        }
    }

    /**
     * Force all dependent libs to use the same version of the libraries.
     * This is used in testing libraries (Espresso).
     * https://code.google.com/p/android-test-kit/issues/detail?id=136
     */
    configurations.all {
        resolutionStrategy {
            force 'org.hamcrest:hamcrest-core:1.3'
            force 'junit:junit:4.12'
            force supportDependencies.supportAnnotation
            force supportDependencies.appCompat
            force supportDependencies.design
            force 'com.google.code.gson:gson:2.7'
            force 'com.google.guava:guava:19.0'
            force 'com.google.code.findbugs:jsr305:3.0.1'
            force 'org.apache.commons:commons-lang3:3.4'
        }
        // removes warnings about commons-logging on build.
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}

// References:
// http://stackoverflow.com/questions/23241681/why-are-there-two-build-gradle-files-in-an-android-studio-project
// http://stackoverflow.com/questions/22356181/how-to-resolve-the-android-studio-error-where-it-cannot-find-the-projects-manif
