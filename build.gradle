/*
 * Copyright (C) 2012-2018 The Android Money Manager Ex Project Team
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 3
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }  // Fabric
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // robolectric
        maven { url "https://clojars.org/repo/" }               // icepick
        mavenCentral()
        maven { url 'https://jitpack.io' }
//        maven { url "https://chaquo.com/maven" } // Chaquopy, Python
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'io.fabric.tools:gradle:1.+'                  // Fabric
//        classpath 'com.chaquo.python:gradle:1.3.1'              // Chaquopy
    }
}

ext {
    compileSdkVersion = 27
    buildToolsVersion = '27.0.3'
    supportLibrary = '27.1.1'

    // https://developer.android.com/topic/libraries/support-library/revisions.html
    supportDependencies = [
            core             :      "com.android.support:support-core-utils:${supportLibrary}",
            support          :      "com.android.support:support-v4:${supportLibrary}",
            support_compat   :      "com.android.support:support-compat:${supportLibrary}",
            appCompat        :      "com.android.support:appcompat-v7:${supportLibrary}",
            cardView         :      "com.android.support:cardview-v7:${supportLibrary}",
            design           :      "com.android.support:design:${supportLibrary}",
            preference       :      "com.android.support:preference-v14:${supportLibrary}",
            recyclerView     :      "com.android.support:recyclerview-v7:${supportLibrary}",
            supportAnnotation:      "com.android.support:support-annotations:${supportLibrary}",
            gridLayout       :      "com.android.support:gridlayout-v7:${supportLibrary}"
    ]
}

// This applies to the projects underneath.
allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }          // fabric
        maven { url 'http://maven.cloudrail.com' }              // cloudrail
        maven { url "https://clojars.org/repo/" }               // icepick
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // butterknife
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    /**
     * Force all dependent libs to use the same version of the libraries.
     * This is used in testing libraries (Espresso).
     * https://code.google.com/p/android-test-kit/issues/detail?id=136
     */
    configurations.all {
        resolutionStrategy {
            force 'org.hamcrest:hamcrest-core:1.3'
            force 'junit:junit:4.12'
            force supportDependencies.supportAnnotation
            force supportDependencies.appCompat
            force supportDependencies.design
            force 'com.google.code.gson:gson:2.8.2'
            force 'com.google.guava:guava:23.0-android'
            force 'com.google.code.findbugs:jsr305:3.0.1'
            force 'org.apache.commons:commons-lang3:3.5'
            force 'commons-collections:commons-collections:3.2.2'
            force 'com.android.support:multidex:1.0.2'
        }
        // removes warnings about commons-logging on build.
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    //
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }
}

/**
 * Build fabric properties file, if missing. Useful for contributors.
 * https://kickstarter.engineering/fabric-for-open-source-android-7743db969d5d
 */
afterEvaluate {
    buildFabricPropertiesIfNeeded()
}
def buildFabricPropertiesIfNeeded() {
    def propertiesFile = file("fabric.properties")
    if (!propertiesFile.exists()) {
        def commentMessage = "This is autogenerated crashlytics property from system environment to prevent key to be committed to source control."
        ant.propertyfile(file: "fabric.properties", comment: commentMessage) {
            entry(key: "apiSecret", value: System.getenv('apiSecret'))
            entry(key: "apiKey", value: System.getenv('apiKey'))
        }
    }
}
